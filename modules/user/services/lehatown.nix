{ pkgs, lib, config, ... }:
let
  cfg = config.lehatown;
  user = "lehatown-server";
  podman = "${pkgs.podman}/bin/podman";
in {
  options.lehatown = {
    enable = lib.mkEnableOption "Enable lehatown";
    ports = with lib;
      with lib.types; {
        ui = mkOption { type = number; };
        update = mkOption { type = number; };
        game = mkOption { type = number; };
      };
  };

  config = lib.mkIf config.lehatown.enable {
    services.podman.enable = true;

    services.podman.containers."stationeers-server-ui" = {
      inherit user;

      image = "ghcr.io/steamserverui/stationeersserverui:latest";
      volumes = [
        "/etc/passwd:/etc/passwd:ro"
        "/etc/group:/etc/group:ro"
        "/home/${user}/stationeers-lehatown/UIMod/config:/app/UIMod/config:rw"
        "/home/${user}/stationeers-lehatown/UIMod/tls:/app/UIMod/tls:rw"
        "/home/${user}/stationeers-lehatown/saves:/app/saves:rw"
        "lehatown_app-data:/app:rw"
      ];

      ports = [
        "${builtins.toString cfg.ports.ui}:8443/tcp"
        "${builtins.toString cfg.ports.update}:27015/tcp"
        "${builtins.toString cfg.ports.update}:27015/udp"
        "${builtins.toString cfg.ports.game}:27016/tcp"
        "${builtins.toString cfg.ports.game}:27016/udp"
      ];

      extraPodmanArgs = [
        "--cpus=2"
        "--memory=11811160064b"
        "--network-alias=stationeers-server-ui"
        "--network=lehatown_default"
        "--userns=keep-id"
      ];

      extraConfig = {
        Unit = {
          Description = "SSUI service";
          PartOf = [ "podman-compose-lehatown-root.target" ];
          Requires = [
            "podman-network-lehatown_default.service"
            "podman-volume-lehatown_app-data.service"
          ];
          After = [
            "podman-network-lehatown_default.service"
            "podman-volume-lehatown_app-data.service"
          ];
        };

        Install.WantedBy = [ "podman-compose-lehatown-root.target" ];
      };
    };

    # Networks
    systemd.user.services."podman-network-lehatown_default" = let
      script = pkgs.writeShellScript "lehatown-network-create" ''
        ${podman} network inspect lehatown_default || ${podman} network create lehatown_default
      '';
    in {
      Service = {
        Type = "oneshot";
        RemainAfterExit = true;
        ExecStart = script;
        ExecStop = "podman network rm -f lehatown_default";
      };

      Install.WantedBy = [ "podman-compose-lehatown-root.target" ];
      Unit.PartOf = [ "podman-compose-lehatown-root.target" ];
    };

    # Volumes
    systemd.user.services."podman-volume-lehatown_app-data" = let
      script = pkgs.writeShellScript "lehatown-volume-create" ''
        ${podman} volume inspect lehatown_app-data || ${podman} volume create lehatown_app-data
      '';
    in {
      Service = {
        Type = "oneshot";
        RemainAfterExit = true;
        ExecStart = script;
      };

      Install.WantedBy = [ "podman-compose-lehatown-root.target" ];
      Unit.PartOf = [ "podman-compose-lehatown-root.target" ];
    };

    # Root service
    # When started, this will automatically create all resources and start
    # the containers. When stopped, this will teardown all resources.
    systemd.user.targets."podman-compose-lehatown-root" = {
      Unit.Description = "Root target generated by compose2nix.";
      Install.WantedBy = [ "multi-user.target" ];
    };
  };
}
